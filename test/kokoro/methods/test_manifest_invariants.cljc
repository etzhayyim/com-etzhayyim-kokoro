(ns kokoro.methods.test-manifest-invariants
  "kokoro — manifest ↔ lexicon-disk invariants (ported from the manifest-reading half of
  70-tools/scripts/audit/test_kokoro_invariants.py). Reads manifest.edn (:actor/manifest
  blob); the jsonld is retired. Lexicon-hygiene checks stay in the Python audit suite."
  (:require [clojure.test :refer [deftest is run-tests]]
            [clojure.string :as str]
            [clojure.edn :as edn]))

(def ^:private here (.getParentFile (java.io.File. ^String *file*)))
(def ^:private repository-root (.. here getParentFile getParentFile getParentFile))
(def ^:private lexdir (java.io.File. repository-root "lex"))

(defn- manifest [] (:actor/manifest (edn/read-string (slurp (java.io.File. repository-root "manifest.edn")))))
(defn- on-disk []
  (->> (.listFiles lexdir) (map #(.getName ^java.io.File %))
       (filter #(str/ends-with? % ".edn"))
       (map #(subs % 0 (- (count %) 4))) set))

(deftest manifest-namespaces-match-disk
  (let [declared (set (map #(last (str/split % #"\.")) (get (manifest) "lexiconNamespaces")))]
    (is (= declared (on-disk))
        (str "manifest namespaces vs disk drifted: " declared " / " (on-disk)))))

(deftest did-name-tier
  (let [m (manifest)]
    (is (= (get m "id") "did:web:kokoro.etzhayyim.com"))
    (is (= (get m "name") "kokoro"))))

(defn -main [& _]
  (let [r (run-tests 'kokoro.methods.test-manifest-invariants)]
    (System/exit (if (zero? (+ (:fail r) (:error r))) 0 1))))
